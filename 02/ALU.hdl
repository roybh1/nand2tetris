// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the output bits:
 * zr = (out == 0, 1, 0)
 * ng = (out < 0,  1, 0)
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // (out == 0, 1, 0)
        ng;      // (out < 0,  1, 0)

    PARTS:
    // handle n, z
    Mux16(a=x, b=false, sel=zx, out=zxl);
    Not16(in=zxl, out=notzxl);
    Mux16(a=zxl, b=notzxl, sel=nx, out=znx);

    Mux16(a=y, b=false, sel=zy, out=zyl);
    Not16(in=zyl, out=notzyl);
    Mux16(a=zyl, b=notzyl, sel=ny, out=zny);

    // handle f
    Add16(a=znx, b=zny, out=xplusy);
    And16(a=znx, b=zny, out=xandy);
    Mux16(a=xandy, b=xplusy, sel=f, out=muxout);
    
    // handle no
    Not16(in=muxout, out=notout);
    Mux16(a=muxout, b=notout, sel=no, out=out, out[0..7]=outf, out[8..15]=outs, out[15]=outmsb);

    // handle zr
    Or8Way(in=outf, out=first8wayor);
    Or8Way(in=outs, out=last8wayor);
    Or(a=first8wayor, b=last8wayor, out=notzr);
    Not(in=notzr, out=zr);

    // handle ng
    And(a=outmsb, b=outmsb, out=ng);
}
